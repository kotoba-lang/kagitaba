(ns kagitaba.item-test
  (:require [clojure.test :refer [deftest is]]
            [kagitaba.item :as item]))

(deftest item*-defaults
  (let [it (item/item* {:category :login :title "GitHub"})]
    (is (= :login (:item/category it)))
    (is (= "GitHub" (:item/title it)))
    (is (= [] (:item/tags it)))
    (is (= :active (:item/state it)))
    (is (false? (:item/favorite? it)))
    (is (= [] (:item/sections it)))))

(deftest section-and-field-shape
  (let [it (item/item* {:category :login :title "GitHub"
                        :sections [{:title "Login"
                                    :fields [{:id "password" :title "password"
                                             :type :concealed :value "hunter2"}
                                            {:id "username" :title "username"
                                             :type :string :value "jun"}]}]})]
    (is (= 1 (count (:item/sections it))))
    (let [fields (:section/fields (first (:item/sections it)))]
      (is (= 2 (count fields)))
      (is (true? (:field/sensitive? (first fields))))
      (is (= :restricted (:field/classification (first fields))))
      (is (= :internal (:field/classification (second fields))))
      (is (false? (:field/sensitive? (second fields)))))))

(deftest valid?-checks-required-keys
  (is (item/valid? (item/item* {:category :login :title "GitHub"})))
  (is (not (item/valid? {:item/title "no category"})))
  (is (not (item/valid? {:item/category :login}))))

(deftest sensitive-fields-collects-across-sections
  (let [it (item/item* {:category :credit-card :title "Visa"
                        :sections [{:title "Card"
                                    :fields [{:id "num" :type :credit-card-number :value "4111"}
                                            {:id "type" :type :credit-card-type :value "visa"}]}
                                   {:title "Extra"
                                    :fields [{:id "pin" :type :concealed :value "1234"}]}]})]
    (is (= #{"num" "pin"} (set (map :field/id (item/sensitive-fields it)))))))

(deftest category-known?
  (is (item/category-known? (item/item* {:category :login :title "x"})))
  (is (not (item/category-known? (item/item* {:category :made-up :title "x"})))))
